buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "com.databases.example"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "Alpha"
    }

    buildTypes {
        def BOOLEAN = "boolean"
        def TRUE = "true"
        def FALSE = "false"
        def REPORT_LOGS = "REPORT_LOGS"
        def REPORT_CRASHES = "REPORT_CRASHES"
        def REPORT_ANALYTICS = "REPORT_ANALYTICS"
        def SHOW_SETTINGS = "SHOW_SETTINGS"

        release {
            buildConfigField BOOLEAN, REPORT_LOGS, FALSE
            buildConfigField BOOLEAN, REPORT_CRASHES, TRUE
            buildConfigField BOOLEAN, REPORT_ANALYTICS, TRUE
            buildConfigField BOOLEAN, SHOW_SETTINGS, FALSE

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = outputFile.name.replace('.apk', "-" + defaultConfig.versionName + ".apk")
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }

        debug {
            buildConfigField BOOLEAN, REPORT_LOGS, TRUE
            buildConfigField BOOLEAN, REPORT_CRASHES, FALSE
            buildConfigField BOOLEAN, REPORT_ANALYTICS, FALSE
            buildConfigField BOOLEAN, SHOW_SETTINGS, TRUE

            ext.enableCrashlytics = false
            debuggable true
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:25.0.0'
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:design:25.0.0'
    compile 'com.jakewharton.timber:timber:4.5.1'
    compile 'net.margaritov.preference.colorpicker.ColorPickerPreference:ColorPickerPreference:1.0.0'
    compile project(':cardsUILib')
    compile 'haibison.android:lockpattern:12.0.0'
    compile project(':wizardLib')
    
    compile('com.crashlytics.sdk.android:crashlytics:2.6.6@aar') {
        transitive = true;
    }
}